apply plugin: 'com.android.application'
apply plugin: 'realm-android'
apply plugin: 'io.fabric'

// Load keystore
def keystorePropertiesFile = file("../../keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    signingConfigs {
        release {
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
        }
    }
    compileSdkVersion 27
    defaultConfig {
        applicationId "com.kksionek.photosyncer"
        minSdkVersion 15
        targetSdkVersion 27
        versionCode 12
        versionName "1.12"
        vectorDrawables.useSupportLibrary = true
        resConfigs "en"
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
    lintOptions {
        checkReleaseBuilds false
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    splits {
        abi {
            enable true
            reset()
            include 'armeabi-v7a', 'x86'
            universalApk false
        }
    }
}

def supportVer = "27.1.1"
def playServicesVer = "15.0.2"
def okhttpVer = "3.5.0"

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'com.simplecityapps:recyclerview-fastscroll:1.0.16'
    implementation "com.android.support:appcompat-v7:$supportVer"
    implementation "com.android.support:customtabs:$supportVer" // to fix issue
    implementation "com.android.support:design:$supportVer"
    implementation "com.google.firebase:firebase-core:$playServicesVer"
    implementation "com.google.firebase:firebase-ads:15.0.1"
    implementation 'com.crashlytics.sdk.android:crashlytics:2.9.2'
    implementation "com.squareup.okhttp3:okhttp:$okhttpVer"
//    implementation "com.squareup.okhttp3:logging-interceptor:$okhttpVer"
    implementation 'com.squareup.picasso:picasso:2.5.2'
    implementation 'io.realm:android-adapters:1.4.0'
    implementation 'io.reactivex:rxjava:1.2.5'
    implementation 'io.reactivex:rxandroid:1.2.1'
    implementation 'org.jsoup:jsoup:1.10.2'
    implementation 'org.apache.commons:commons-lang3:3.5'
    testImplementation 'junit:junit:4.12'
}
apply plugin: 'com.google.gms.google-services'